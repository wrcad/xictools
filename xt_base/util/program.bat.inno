@echo off
@rem   ---------------------------------------------------------------------
@rem   PROGRAM.BAT:  Command file to start XicTools programs.

@rem   This allows the programs to start "out of the box", without any
@rem   further setup.  This is used as a wrapper to call programs
@rem   that require DLLs from MSYS2 started outside of the MSYS2
@rem   environment.

@rem   This version assumes that the program was installed with the
@rem   inno installer, and will not work otherwise.

@rem   INSTRUCTIONS
@rem   Copy this file to a new name.
@rem   The name of this file must be the same as a Whiteley
@rem   Research program, e.g., "xic", "wrspice", etc.  The extension
@rem   should be "bat".  Example:  xic.bat
@rem   Do this for each of your XicTools programs that require
@rem   DLLs from the bundle.  These are xic and wrspice, plus
@rem   mozy, xeditor.
@rem   These .bat files are automatically installed in recent
@rem   distributions.

@rem   The programs can then be started by executing the .bat files
@rem   These files can reside anywhere.

@rem   Feel free to hack this script as needed.
@rem   ---------------------------------------------------------------------

@rem   ---------------------------------------------------------------------
@rem   Set the progname variable to the base name of this script file.
@rem   For example, if this file is named "foo.bat", progname will be
@rem   set to "foo".

set progname=%~n0
@rem   ---------------------------------------------------------------------

@rem   ---------------------------------------------------------------------
@rem   We need a special name as stored in the Registry.  This is set by
@rem   the installer.

if %progname%==xic (
    set appname=xic_is1
) else (
    if %progname%==wrspice (
        set appname=wrspice_is1
    ) else (
        if %progname%==httpget (
            set appname=mozy_is1
        ) else (
            if %progname%==mozy (
                set appname=mozyis1
            ) else (
                if %progname%==xeditor (
                    set appname=mozy_is1
                )
            )
        )
    )
)

@rem   ---------------------------------------------------------------------
@rem   Set the prefix variable to the InstallPrefix saved in the Registry.
@rem   This is where the user installed the program.  This is rather opaque
@rem   and magical.  The default is c:\usr\local\

@rem   32-bit app in a 64-bit registry view, e.g., Cygwin64 or native64.
set key=HKLM\Software\WOW6432Node\Microsoft\Windows\CurrentVersion\Uninstall

reg query %key%\%appname% /v InstallLocation > NUL 2>&1
if ERRORLEVEL 1 (
@rem   32-bit app in a 32-bin registtry view, e.g., Cygwin32, or 32-bit
@rem   Windows if there is such a thing anymore.
    set key=HKLM\Software\Microsoft\Windows\CurrentVersion\Uninstall
)
reg query %key%\%appname% /v InstallLocation > NUL 2>&1
if ERRORLEVEL 1 (
    echo Error:  %progname% installation location not found in registry.
    exit
)

for /f "Tokens=2,*" %%A in (
    'reg query %key%\%appname% /v InstallLocation'
) do (
    set prefix=%%B
)

@rem   We would like to find the MSYS2/MINGW64 bin somehow.  For now,
@rem   just assume the default location.

set mingw_dll_path=c:\msys64\mingw64\bin

@rem   ---------------------------------------------------------------------

@rem   ---------------------------------------------------------------------
@rem   Locally modify the search path so that the program can find
@rem   the MINGW DLLs, before any others in the PATH.

setlocal
PATH=%mingw_dll_path%;%PATH%
@rem   ---------------------------------------------------------------------

@rem   ---------------------------------------------------------------------
@rem   Execute the program, with the same arguments (if any) that were
@rem   given to this script.

if %progname%==xic (
    "%prefix%xictools\xic\bin\xic.exe" %*
) else (
    if %progname%==wrspice (
        "%prefix%xictools\wrspice\bin\wrspice.exe" %*
    ) else (
        "%prefix%xictools\bin\%progname%.exe" %*
    )
)
    
endlocal
@rem   ---------------------------------------------------------------------
@rem   End of script.

